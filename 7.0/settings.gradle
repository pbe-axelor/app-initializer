pluginManagement {
  repositories {
    mavenCentral() {
      content {
        excludeGroup 'com.axelor'
      }
    }
    maven {
      url 'https://repository.axelor.com/nexus/repository/maven-public/'
    }
  }
  plugins {
    id 'com.axelor.app' version '6.1.3'
  }
}


dependencyResolutionManagement {
  repositories {
    mavenCentral() {
      content {
        excludeGroup 'com.axelor'
      }
    }
    // Axelor public repo
    maven {
      url 'https://repository.axelor.com/nexus/repository/maven-public/'
    }
    // Axelor enterprise repo
    if (settings.hasProperty('axelorMavenUsername')) {
      maven {
          name = "maven-enterprise"
          url = "https://repository.axelor.com/nexus/repository/maven-enterprise"
          credentials {
              username = settings.getProperty( 'axelorMavenUsername' )
              password = settings.getProperty( 'axelorMavenPassword' )
          }
      }
    }
    // Declare the Node.js download repository
    ivy {
      name = "Node.js"
      setUrl("https://nodejs.org/dist/")
      patternLayout {
        artifact("v[revision]/[artifact](-v[revision]-[classifier]).[ext]")
      }
      metadataSources {
        artifact()
      }
      content {
        includeModule("org.nodejs", "node")
      }
    }
  }
}

rootProject.name = "{{PROJECT_NAME}}"
